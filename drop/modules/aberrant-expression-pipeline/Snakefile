AE_WORKDIR = cfg.AE.getWorkdir(str_=False)

AE_index_name = "aberrant-expression-pipeline"
AE_index_input, AE_index_output, AE_graph_file, _ = createIndexRule(
    scriptsPath=str(AE_WORKDIR),
    index_name=AE_index_name
)

rule aberrantExpression:
    input: AE_index_input, AE_graph_file
    output: AE_index_output
    run: ci(str(AE_WORKDIR), AE_index_name)

rule aberrantExpression_dependency:
    output: AE_graph_file
    shell: "snakemake --rulegraph {AE_index_output} | dot -Tsvg -Grankdir=TB > {output}"

rule aberrantExpression_bamStats:
    input: 
        bam = lambda wildcards: sa.getFilePath(wildcards.sampleID, "RNA_BAM_FILE"),
        ucsc2ncbi =AE_WORKDIR / "resource" / "chr_UCSC_NCBI.txt"
    output:
        cfg.processedDataDir / "aberrant_expression" / "{annotation}" / "coverage" / "{sampleID}.tsv"
    params:
        samtools = config["tools"]["samtoolsCmd"]
    shell:
        """
        # identify chromosome format
        if {params.samtools} idxstats {input.bam} | grep -qP "^chr";
        then
            chrNames=$(cut -f1 {input.ucsc2ncbi} | tr '\n' '|')
        else
            chrNames=$(cut -f2 {input.ucsc2ncbi} | tr '\n' '|')
        fi

        # write coverage from idxstats into file
        count=$({params.samtools} idxstats {input.bam} | grep -E "^($chrNames)" | \
                cut -f3 | paste -sd+ - | bc)

        echo -e "{wildcards.sampleID}\t${{count}}" > {output}
        """

rule aberrantExpression_mergeBamStats:
    input: 
        lambda w: expand(cfg.getProcessedDataDir() +
            "/aberrant_expression/{{annotation}}/coverage/{sampleID}.tsv",
            sampleID=sa.getIDsByGroup(w.dataset), assay="RNA")
    output:
        cfg.getProcessedDataDir() + "/aberrant_expression/{annotation}/outrider/{dataset}/bam_coverage.tsv"
    params:
        exIDs = lambda w: sa.getIDsByGroup(w.dataset, assay="GENE_COUNT")
    run:
        with open(output[0], "w") as bam_stats:
            bam_stats.write("sampleID\trecord_count\n")
            for f in input:
                bam_stats.write(open(f, "r").read())
            for eid in params.exIDs:
                bam_stats.write(f"{eid}\tNA\n")
